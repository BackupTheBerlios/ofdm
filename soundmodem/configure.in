AC_INIT(acconfig.h)
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(soundmodem, 0.6)
AM_CONFIG_HEADER(config.h)

dnl AC_CHECK_TOOL()

AC_PROG_MAKE_SET
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
dnl AC_PROG_RANLIB
AC_C_CONST
AC_C_INLINE
AC_HEADER_STDC
AC_FUNC_ALLOCA

AC_CHECK_PROG(RANLIB, ranlib, ranlib, :)
AC_CHECK_PROG(DLLTOOL, dlltool, dlltool, dlltool)
AC_CHECK_PROG(AS, as, as, as, as)
AC_CHECK_PROG(AR, ar, ar, ar, ar)
AC_CHECK_PROG(WINDRES, windres, windres, i686-pc-cygwin-windres)

AC_CYGWIN
AC_MINGW32
AC_EXEEXT
AC_OBJEXT

dnl check for cross compiler path
if test x$ac_cv_prog_cc_cross = xyes; then
  AC_MSG_CHECKING(for cross compiler path)
  if test -d /usr/local/cross/i686-pc-cygwin; then
    CROSSCOMPPATH=/usr/local/cross/i686-pc-cygwin
  elif test -d /usr/local/cygwin/i686-pc-cygwin; then
    CROSSCOMPPATH=/usr/local/cygwin/i686-pc-cygwin
  else
    AC_ERROR("cross compiler not found")
  fi
  AC_MSG_RESULT($CROSSCOMPPATH)
fi

if test x$ac_cv_cygwin != xyes -a x$ac_cv_mingw32 != xyes; then
  AC_CHECK_LIB(m,sqrt)
fi
AC_CHECK_LIB(pthread32,pthread_create,LIBTHREAD="$LIBTHREAD -lpthread32",AC_CHECK_LIB(pthread,pthread_create,LIBTHREAD="$LIBTHREAD -lpthread"))
AC_CHECK_LIB(posix4,sched_setscheduler)
AC_CHECK_LIB(uuid,GUID_NULL)
AC_CHECK_FUNCS(getopt_long,,LIBOBJS="$LIBOBJS getopt.o getopt1.o")
AC_CHECK_HEADERS(sys/audioio.h stropts.h sys/conf.h sys/soundcard.h sys/ioctl.h time.h inttypes.h)
AC_CHECK_HEADERS(pty.h getopt.h syslog.h sched.h linux/if.h linux/ax25.h linux/sockios.h sys/ioccom.h linux/ppdev.h)
AC_CHECK_FUNCS(snprintf vsnprintf syslog vsyslog openlog closelog)
AC_CHECK_FUNCS(mlockall)

AC_MSG_CHECKING(for signed bittypes)
signedbittypes=no
AC_TRY_COMPILE([#include <sys/types.h>], 
     [ int8_t a; int16_t c; int32_t e; ],
     AC_DEFINE(HAVE_SIGNED_BITTYPES) signedbittypes=yes)
AC_MSG_RESULT($signedbittypes)

AC_MSG_CHECKING(for unsigned bittypes)
unsignedbittypes=no
AC_TRY_COMPILE([#include <sys/types.h>], 
     [ u_int8_t b; u_int16_t d; u_int32_t f; ],
     AC_DEFINE(HAVE_UNSIGNED_BITTYPES) unsignedbittypes=yes)
AC_MSG_RESULT($unsignedbittypes)

AC_TYPE_SIGNAL

AC_MSG_CHECKING(for M_PI)
mpi=no
AC_TRY_COMPILE([#include <math.h>], 
     [ double f = M_PI; ],
     mpi=yes,AC_DEFINE(M_PI,3.14159265358979323846))
AC_MSG_RESULT($mpi)

AC_MSG_CHECKING(for M_LOG10E)
mlog10e=no
AC_TRY_COMPILE([#include <math.h>], 
     [ double f = M_LOG10E; ],
     mlog10e=yes,AC_DEFINE(M_LOG10E,0.43429448190325182765))
AC_MSG_RESULT($mlog10e)

dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

if test x$ac_cv_cygwin = xyes -o x$ac_cv_mingw32 = xyes; then
  AC_DEFINE(WIN32)
  LIBS="$LIBS -ldsound -lgdi32"
fi

if test x$ac_cv_prog_cc_cross = xyes; then
  gtk=no
  xlibs="$LIBS"
  LIBS="$LIBS -L$CROSSCOMPPATH/gtk/lib"
  AC_CHECK_LIB(gtk,gtk_main,gtk=yes)
  LIBS="$xlibs"
  if test x$gtk = xyes; then
    GTK_CFLAGS="-I$CROSSCOMPPATH/gtk/include -I$CROSSCOMPPATH/gtk/include/glib -I$CROSSCOMPPATH/gtk/include/gdk"
    GTK_LIBS="-L$CROSSCOMPPATH/gtk/lib -lgtk -lgdk -lglib"
    PTHREAD_CFLAGS="-I$CROSSCOMPPATH/gtk/include"
    PTHREAD_LIBS="-L$CROSSCOMPPATH/gtk/lib"
    AUDIOFILE_CFLAGS="-I$CROSSCOMPPATH/audiofile/include"
    AUDIOFILE_LIBS="-L$CROSSCOMPPATH/audiofile/lib -laudiofile"
  fi
else
  AM_PATH_GTK(1.2.0,gtk=yes,AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))
  AM_PATH_AUDIOFILE(0.1.0)
fi

if test x$ac_cv_cygwin = xyes -o x$ac_cv_mingw32 = xyes; then
  XMLCFLAGS=
  XMLLIBS=
else
  xlibs=$LIBS
  LIBS=
  AC_CHECK_LIB(util,openpty)
  AC_CHECK_FUNCS(openpty,LIBTHREAD="$LIBTHREAD $LIBS",LIBOBJS="$LIBOBJS openpty.o")
  LIBS=$xlibs

  AC_PATH_PROG(XML_CONFIG,xml-config,:)
  if test "$XML_CONFIG" = ":" ; then
    AC_MSG_ERROR(Could not find xml-config; libxml installed?)
  fi
  XMLCFLAGS=`$XML_CONFIG --cflags`
  XMLLIBS=`$XML_CONFIG --libs`
  xcflags=$CFLAGS
  xlibs=$LIBS
  LIBS="$LIBS $XMLLIBS"
  CFLAGS="$CFLAGS $XMLCFLAGS"
  AC_CHECK_LIB(xml, xmlNewDoc,, AC_MSG_ERROR(Cannot link libxml))
  LIBS=$xlibs
  CFLAGS=$xcflags
fi

xcflags="$CFLAGS"
CFLAGS="$CFLAGS -I$srcdir/directx/include -I$srcdir/directx/include/directx6"
AC_MSG_CHECKING(for DirectX includes)
directx=no
AC_TRY_COMPILE([#include <directx.h>], 
     [ LPDIRECTSOUND dsplay; LPDIRECTSOUNDCAPTURE dsrec; ],
     AC_DEFINE(HAVE_DIRECTX) directx=yes)
AC_MSG_RESULT($directx)
CFLAGS="$xcflags"

AC_MSG_CHECKING(for ifr_newname in struct ifreq)
ifrnewname=no
AC_TRY_COMPILE([#include <linux/if.h>],
     [struct ifreq ifr; ifr.ifr_newname[0]=0; ], AC_DEFINE(HAVE_IFRNEWNAME) ifrnewname=yes)
AC_MSG_RESULT($ifrnewname)

xlibs=$LIBS
LIBS=
AC_CHECK_FUNCS(vsnprintf,,LIBOBJS="$LIBOBJS vsnprintf.o")
AC_CHECK_FUNCS(random,,LIBOBJS="$LIBOBJS random.o")
LIBS=$xlibs

AC_MSG_CHECKING(for MKISS (N_AX25 line discipline))
mkiss=no
AC_EGREP_CPP(yes,
     [#include <sys/ioctl.h>
     #ifdef N_AX25
       yes
     #endif
     ], AC_DEFINE(HAVE_MKISS) mkiss=yes)
AC_MSG_RESULT($mkiss)

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

AC_ARG_ENABLE(mmx,
[  --enable-mmx            Utilize MMX(tm) instructions if available (x86 only)],
[case "${enableval}" in
  yes) usemmx=true ;;
  no)  usemmx=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-mmx) ;;
esac],[usemmx=false])

AC_ARG_ENABLE(vis,
[  --enable-vis            Utilize VIS(tm) instructions if available (ultrasparc only)],
[case "${enableval}" in
  yes) usevis=true ;;
  no)  usevis=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-vis) ;;
esac],[usevis=false])

if test x$usemmx = xtrue; then
    AC_DEFINE(USEMMX)
fi

if test x$usevis = xtrue; then
    AC_DEFINE(USEVIS)
    dnl CFLAGS="$CFLAGS -mv8plus -mvis -Wa,-xarch=v8plusa"
    CFLAGS="$CFLAGS -Wa,-xarch=v8plusa"
fi

AM_CONDITIONAL(USEMMX, test x$usemmx = xtrue)
AM_CONDITIONAL(USEVIS, test x$usevis = xtrue)
AM_CONDITIONAL(CROSSCOMP, test x$cross_compiling = xyes)
AM_CONDITIONAL(DIRECTX, test x$directx = xyes)
AM_CONDITIONAL(WIN32, test x$ac_cv_cygwin = xyes -o x$ac_cv_mingw32 = xyes)

AC_SUBST(HAVE_BITTYPES)
AC_SUBST(HAVE_DIRECTX)
AC_SUBST(HAVE_MKISS)
AC_SUBST(HAVE_IFRNEWNAME)
AC_SUBST(M_PI)
AC_SUBST(LIBOBJS)
AC_SUBST(LIBTHREAD)
AC_SUBST(USEMMX)
AC_SUBST(USEVIS)
AC_SUBST(XMLCFLAGS)
AC_SUBST(XMLLIBS)
AC_SUBST(GTK_LIBS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(PTHREAD_LIBS)
AC_SUBST(PTHREAD_CFLAGS)
AC_SUBST(WIN32)

AC_OUTPUT([Makefile
po/Makefile.in
intl/Makefile
directx/Makefile
libmisc/Makefile
matlib/Makefile
afsk/Makefile
fsk/Makefile
pammodem/Makefile
pskmodem/Makefile
newqpsk/Makefile
p3dmodem/Makefile
soundcard/Makefile
flexdrv/Makefile
doc/Makefile
configapp/Makefile
configapp/src/Makefile])
